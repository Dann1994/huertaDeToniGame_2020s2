import wollok.game.*
import toni.*
import plantas.*
import pachamama.*

program huertaDeToni {
	
	game.width(15)
	game.height(15)
	
	// Configuramos una imagen de fondo, que se repite para cada celda del tablero.
	game.ground("tierra.png")
	
	// Agregamos al amigo Toni.
	/** (Danny)
      *Cambié "addVisual" por "addVisualCharacter"
      *este método agrega a "toni" y le da por defecto los
      *movimietos con las flechas, sin necesidad de tener que
      *programarlos.
      */
  //game.addVisual(toni) 
	game.addVisualCharacter(toni) 
	
	
	/*
	 * Acá usamos addVisualIn en vez de addVisual, y por esto 
	 * no es necesario que la pachamama conozca su position.
	 */
	game.addVisualIn(pachamama, game.at(0, 13));
	
	/* 
	 * Agregamos una fila de maices. Ojo al regar, que Toni no va a conocer
	 * estas plantas.
	 * 
	 * En este ejemplo también usamos addVisualIn, por lo tanto
	 * el Maiz solo define la imagen.
	 * 
	 * De yapa, mostramos cómo agregar varios objetos de una utilizando
	 * un rango (1..13), que equivale a la lista [1, 2, 3, ..., 13].
	 */
	// (1..13).forEach { x => game.addVisualIn(new Maiz(), game.at(x, 5)) }
		
	
	/**controles */ 
	
	/**Agraga un maiz al presionar "M" en la posición actual de Toni 
       (Danny)*/
	keyboard.m().onPressDo ({
		var maiz = new Maiz(position = toni.position().right(1))
		toni.sembrarPlanta(maiz)
		game.addVisual(maiz)
	})
	
	/**Agraga un trigo al presionar "M" en la posición actual de Toni 
       (Danny)*/
	keyboard.t().onPressDo ({
		var trigo = new Trigo(position = toni.position().right(1))
		toni.sembrarPlanta(trigo)
		game.addVisual(trigo)

	})
	
	/**Agraga un tomaco al presionar "O" en la posición actual de Toni 
       (Danny)*/
	keyboard.o().onPressDo ({
		var tomaco = new Tomaco(position = toni.position().right(1))
		toni.sembrarPlanta(tomaco)
		game.addVisual(tomaco)
	})
	
	/**Riega todas las plnatas del tablero */
	keyboard.a().onPressDo({ toni.regarLasPlantas() })
	
	/**Riega la planta en la posición actual de Toni y la hce crecer a su siguiente etapa (Danny) */
	keyboard.r().onPressDo({ game.colliders(toni).forEach({ p => toni.regarPlanta(p) }) })
	
	/**Cochecha todas las plantas listas del tablero que esten listas (Danny) */
	keyboard.x().onPressDo({ 
		toni.plantasListasParaCosechar().forEach({ p => game.removeVisual(p) })
		toni.cosecharTodo()
	})
	
	/**Cosecha las plantas listas en la posiscion actaual de Toni (Danny) */
	keyboard.c().onPressDo({ 
		game.colliders(toni).forEach({ p => toni.cosechar(p); 
			if (toni.plantasCosechadas().contains(p)) {
				game.removeVisual(p)
			} 
		})
	})
	
	/**Vende todas las plantas cosechadas hasta el momento (Danny) */
	keyboard.v().onPressDo({ toni.venderCosecha() })
	
	/**Devuelve la cantidad de oro de toni y cuantas plantas le queda para vender (Danny) */
	keyboard.space().onPressDo({ 
		game.say(toni, "Tengo " + toni.oro() + " monedas" + 
		" y me qudan " + toni.plantasCosechadas().size() + " plantas para vender" )
	})
	
	/** Al presionar F, muestra el mensaje que está fumigado. (Caro) */
	keyboard.f().onPressDo({ pachamama.fumigar() })
	
	/** Al presionar L, logrará que llueva y las plantas se rieguen (Caro) */
	keyboard.l().onPressDo({ pachamama.llover() })
	
	game.start()
}