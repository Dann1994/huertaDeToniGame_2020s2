import wollok.game.*
import toni.*
import plantas.*
import pachamama.*
import mercados.*

program huertaDeToni {
	
	/**Tablero del juego */
	game.title("La Huerta de Toni")
	
	game.width(15)
	
	game.height(15)

	game.ground("tierra.png")
	
	/*******************************************************************************************/
	
	/**Visuales */
    game.addVisual(toni) 
	
	game.addVisual(pachamama); (1..13).forEach { x => game.addVisualIn(new Maiz(), game.at(x, 5)) }
	
	/**Mercados */
	game.addVisual(new Mercado(monedas=360))
	game.addVisual(new Mercado(monedas=10, position = game.at(9,4)))	
	game.addVisual(new Mercado(monedas=1000, position = game.at(7,6)))
	
	/*******************************************************************************************/
	
	/**Controles movimieto tipo "Pac man" */
	
	/**Mueve a toni en dirección al norte, al atravesar el borde del tablero, este 
      *aparecerá en el borde sur */
	keyboard.up().onPressDo ({
		if (toni.estaEnBordeNorte()) {
			toni.position(toni.position().down(game.height() - 1))
		} else { toni.moveteArriba() }
	})	
	/**Mueve a toni en dirección al sur, al atravesar el borde del tablero, este 
      *aparecerá en el borde norte */
	keyboard.down().onPressDo ({
		if (toni.estaEnBordeSur()) {
			toni.position(toni.position().up(game.height() - 1))
		} else { toni.moveteAbajo() }	
	})	
	/**Mueve a toni en dirección al oeste, al atravesar el borde del tablero, este 
      *aparecerá en el borde este */
	keyboard.left().onPressDo ({
		if (toni.estaEnBordeOeste()) {
			toni.position(toni.position().right(game.width() - 1))
		} else { toni.moveteIzquierda() }	
	})	
	/**Mueve a toni en dirección al este, al atravesar el borde del tablero, este 
      *aparecerá en el borde oeste */
	keyboard.right().onPressDo ({
		if (toni.estaEnBordeEste()) {
			toni.position(toni.position().left(game.width() - 1))
		} else { toni.moveteDerecha() }
	})
	
	/*******************************************************************************************/
	
	/**Controles de sembrado */
	
	/**Agraga un maiz al presionar "M" en la posición actual de Toni */
	keyboard.m().onPressDo ({
		if (toni.estaEnCeldaVacia()) {
			var maiz = new Maiz(position = toni.position())
		    toni.sembrarPlanta(maiz)
		} else (game.say(toni, "No puedo sembrar acá"))
		
	})
	/**Agraga un trigo al presionar "T" en la posición actual de Toni */
	keyboard.t().onPressDo ({
		if (toni.estaEnCeldaVacia()) {
		    var trigo = new Trigo(position = toni.position())
		    toni.sembrarPlanta(trigo)
		} else (game.say(toni, "No puedo sembrar acá"))

	})
	/**Agraga un tomaco al presionar "O" en la posición actual de Toni */
	keyboard.o().onPressDo ({
		if (toni.estaEnCeldaVacia()) {
		    var tomaco = new Tomaco(position = toni.position())
		    toni.sembrarPlanta(tomaco)
		} else (game.say(toni, "No puedo sembrar acá"))
		
	})
	
	/*******************************************************************************************/
	
	/**Controles de riego */
	
	/**Riega todas las plnatas del tablero */
	keyboard.a().onPressDo({ toni.regarLasPlantas() })
	
	/**Riega la planta en la posición actual de Toni y la hce crecer a su siguiente etapa (Danny) */
	keyboard.r().onPressDo({
		if (toni.sobreUnaPlanta()) {
			toni.regarPlanta(game.uniqueCollider(toni))
		}
		
	})
	
	/*******************************************************************************************/
	
	/**Controles de cosecha y venta */
	
	/**Cochecha todas las plantas listas del tablero que esten listas */
	keyboard.x().onPressDo({ 
		toni.cosecharTodo()
	})
	
	/**Cosecha las plantas listas en la posición actaual de Toni, si no
      *está lista para cosechar, toni avisa y no cosecha nada, y si no hay
      *una planta en la celda actual, avisa que no hay una planta. */
	keyboard.c().onPressDo({ 
		if (toni.sobreUnaPlanta()) {
			toni.cosechar(game.uniqueCollider(toni))
		} else { 
			game.say(toni, "No hay una planta acá") 
		}	
	})
	
	/**Vende todas las plantas cosechadas hasta el momento solo si toni se encuentra en un mercado */
	keyboard.v().onPressDo({ toni.venderCosecha() })
	
	/**Devuelve la cantidad de oro de toni y cuantas plantas le queda para vender */
	keyboard.space().onPressDo({ 
		game.say(toni, "Tengo " + toni.oro() + " monedas" + 
		" y me quedan " + toni.plantasCosechadas().size() + " plantas para vender" )
	})
	
	/*******************************************************************************************/
	
	/**Fumigar y llover */
	
	/** Al presionar F, fumiga y toni muestra el mensaje que está fumigado. */
	keyboard.f().onPressDo({ 
		pachamama.fumigar()
		game.say(toni, "Está fumigado")
	})
	
	/** Al presionar L, logrará que llueva y las plantas se rieguen */
	keyboard.l().onPressDo({ pachamama.llover() })
	
	/*******************************************************************************************/
	
	/**Ofrenda a la pachamama */
	
	/**Al chocar con la pacha, Toni hace una ofrenda */
	game.whenCollideDo(toni, { obj => 
		if (toni.agunaPlantaLista() && obj.equals(pachamama)) {
			toni.hacerOfrenda()
		    game.say(toni, "Gracias,Pacha")
		    pachamama.cambiarDePosicion()
		}
	})
	
	/*******************************************************************************************/
	
	game.start()
}